module

public import Kraken.X64.PrintIntel

public section
/-!
# AT&T printer
Prints Kraken x64 syntax in the AT&T dialect accepted by `Kraken.X64.Parser.parse`.
The intended property is `parse (toATT p) = .ok p` for every program `p` that `parse`
can produce (see `PrintATTTests.lean`). Values outside the parser's image (e.g. memory
operands without a base register, or `ConstExpr` arithmetic) are still printed, but
won't round-trip.

Mnemonics carry a width suffix whenever the operands may not determine the width
(immediates, memory), and are unsuffixed when a register operand always does.
-/
namespace Kraken.X64.ATT

def suffix (w : Width) : String := w.attSuffix

def reg {w} (r : Reg w) : String := "%" ++ r.toStr

def avxReg {w} (r : AvxReg w) : String := "%" ++ toString r

def const : ConstExpr → String
  | .label l => l
  | .int64 i => toString i
  | e => toString e

def addr (aw : Width) (a : AddrExpr) : String :=
  let disp := match a.base, a.disp with
    | some .rip, .sub e .after_current_instruction => const e
    | _, .int64 0 => ""
    | _, d => const d
  let base := match a.base with
    | some (.reg r) => reg (.low r aw)
    | some .rip => "%rip"
    | none => ""
  let idx := match a.idx with
    | some ⟨r, s⟩ => s!",{reg (.low r aw)},{s.bytes}"
    | none => ""
  s!"{disp}({base}{idx})"

def rm {w} (aw : Width) : RegOrMem w → String
  | .reg r => reg r
  | .mem a => addr aw a

def avxRm {w} (aw : Width) : AvxRegOrMem w → String
  | .avx r => avxReg r
  | .mem a => addr aw a

/-- A source operand of `bytes?` bytes if `some` (narrower than the vector): a register is then
named at 128 bits. -/
def avxSrc {w} (aw : Width) (bytes? : Option Nat) : AvxRegOrMem w → String
  | .avx r => if bytes?.isSome then avxReg (r.as .W128) else avxReg r
  | .mem a => addr aw a

def operand {w} (aw : Width) : Operand w → String
  | .regOrMem x => rm aw x
  | .imm (.label l) => l
  | .imm e => "$" ++ const e

def count : ShiftCountExpr → String
  | .cl => "%cl"
  | .imm8 e => "$" ++ const e

def target (aw : Width) : RelRegOrMem → String
  | .rel (.sub e .after_current_instruction) => const e
  | .rel e => const e
  | .reg r => reg r
  | .mem a => addr aw a

def operation {w} (aw : Width) (op : Operation w) : String :=
  let s := suffix w
  let one (mn a : String) := s!"{mn}{s} {a}"
  let two (mn a b : String) := s!"{mn}{s} {a}, {b}"
  match op with
  | .mov d x => two "mov" (operand aw x) (rm aw d)
  | .movsx (w' := w') d x => match x with
    | .reg _ =>
      if w' == .W32 && w == .W64 then s!"movslq {rm aw x}, {rm aw d}"
      else s!"movsx {rm aw x}, {rm aw d}"
    | .mem _ => s!"movs{suffix w'}{s} {rm aw x}, {rm aw d}"
  | .movzx (w' := w') d x => match x with
    | .reg _ => s!"movzx {rm aw x}, {rm aw d}"
    | .mem _ => s!"movz{suffix w'}{s} {rm aw x}, {rm aw d}"
  | .push x => one "push" (operand aw x)
  | .pop d => one "pop" (rm aw d)
  | .leave => "leave"
  | .pushf => "pushfq"
  | .popf => "popfq"
  | .setcc cc d => s!"set{cc} {rm aw d}"
  | .cmovcc cc d x => s!"cmov{cc} {rm aw x}, {reg d}"
  | .xchg d x => s!"xchg{s} {reg x}, {rm aw d}"
  | .xadd d x => s!"xadd{s} {reg x}, {rm aw d}"
  | .cmpxchg d x => s!"cmpxchg{s} {reg x}, {rm aw d}"
  | .cmpxchg8b a => s!"cmpxchg8b {addr aw a}"
  | .cmpxchg16b a => s!"cmpxchg16b {addr aw a}"
  | .ud2 => "ud2"
  | .int3 => "int3"
  | .hlt => "hlt"
  | .clc => "clc"
  | .stc => "stc"
  | .cmc => "cmc"
  | .lahf => "lahf"
  | .sahf => "sahf"
  | .cld => "cld"
  | .std => "std"
  | .movs rep => s!"{rep.toStrPrefix}movs{s}"
  | .stos rep => s!"{rep.toStrPrefix}stos{s}"
  | .lods rep => s!"{rep.toStrPrefix}lods{s}"
  | .cmps rep => s!"{rep.toStrPrefix}cmps{s}"
  | .scas rep => s!"{rep.toStrPrefix}scas{s}"
  | .lea d a => s!"lea {addr aw a}, {reg d}"
  | .add d x => two "add" (operand aw x) (rm aw d)
  | .adc d x => two "adc" (operand aw x) (rm aw d)
  | .adcx d x => s!"adcx {rm aw x}, {reg d}"
  | .adox d x => s!"adox {rm aw x}, {reg d}"
  | .inc d => one "inc" (rm aw d)
  | .dec d => one "dec" (rm aw d)
  | .neg d => one "neg" (rm aw d)
  | .sub d x => two "sub" (operand aw x) (rm aw d)
  | .sbb d x => two "sbb" (operand aw x) (rm aw d)
  | .cmp a b => two "cmp" (operand aw b) (rm aw a)
  | .mul x => one "mul" (rm aw x)
  | .mulx hi lo x => s!"mulx {rm aw x}, {reg lo}, {reg hi}"
  | .imul1 x => one "imul" (rm aw x)
  | .imul none a b => two "imul" (operand aw b) (rm aw a)
  | .imul (some d) a b => s!"imul{s} {operand aw b}, {rm aw a}, {rm aw d}"
  | .div x => one "div" (rm aw x)
  | .idiv x => one "idiv" (rm aw x)
  | .cbw => match w with
    | .W8 | .W16 => "cbtw"
    | .W32 => "cwtl"
    | .W64 => "cltq"
  | .cwd => match w with
    | .W8 | .W16 => "cwtd"
    | .W32 => "cltd"
    | .W64 => "cqto"
  | .test a b => two "test" (operand aw b) (rm aw a)
  | .and d x => two "and" (operand aw x) (rm aw d)
  | .not d => one "not" (rm aw d)
  | .or d x => two "or" (operand aw x) (rm aw d)
  | .xor d x => two "xor" (operand aw x) (rm aw d)
  | .shl d c => two "shl" (count c) (rm aw d)
  | .shr d c => two "shr" (count c) (rm aw d)
  | .sar d c => two "sar" (count c) (rm aw d)
  | .shld d x c => s!"shld {count c}, {reg x}, {rm aw d}"
  | .shrd d x c => s!"shrd {count c}, {reg x}, {rm aw d}"
  | .rol d c => two "rol" (count c) (rm aw d)
  | .ror d c => two "ror" (count c) (rm aw d)
  | .rcl d c => two "rcl" (count c) (rm aw d)
  | .rcr d c => two "rcr" (count c) (rm aw d)
  | .bswap d => s!"bswap {reg d}"
  | .movbe d x => two "movbe" (rm aw x) (rm aw d)
  | .crc32 (w' := w') d x => s!"crc32{suffix w'} {rm aw x}, {reg d}"
  | .rorx d x c => s!"rorx{s} ${const c}, {rm aw x}, {reg d}"
  | .un op d x => s!"{Mnemonic.name op} {rm aw x}, {reg d}"
  | .bin op d a b =>
    if op.src2First then s!"{Mnemonic.name op} {rm aw b}, {reg a}, {reg d}"
    else s!"{Mnemonic.name op} {reg a}, {rm aw b}, {reg d}"
  | .bt op d b => two (Mnemonic.name op) (operand aw b) (rm aw d)
  | .jcc cc l => s!"j{cc} {l}"
  | .jrcxz l => s!"jrcxz {l}"
  | .jecxz l => s!"jecxz {l}"
  | .loop .none l => s!"loop {l}"
  | .loop .e l => s!"loope {l}"
  | .loop .ne l => s!"loopne {l}"
  | .jmp t => s!"jmp {target aw t}"
  | .call t => s!"call {target aw t}"
  | .ret => "ret"
  | .nop n => s!"nop {n}"
  | .nopalign a none => s!".align {a}"
  | .nopalign a (some p) => s!".align {a}, {p}"
  | .hint op => Mnemonic.name op
  | .nopm x => s!"nop{s} {rm aw x}"
  | .memHint op a => s!"{Mnemonic.name op} {addr aw a}"
  | .movnti dst src => s!"movnti {reg src}, {addr aw dst}"
  | .xlat => if aw == .W32 then "xlatb (%ebx)" else "xlatb"

def simdCount (aw : Width) : SimdCount → String
  | .imm e => "$" ++ const e
  | .reg x => avxRm aw x

def optImm : Option ConstExpr → String
  | some i => s!"${const i}, "
  | none => ""

def cvtSuffix {gw : Width} (op : SimdInsertOp) (src : RegOrMem gw) : String :=
  if (op == .cvtsi2ss || op == .cvtsi2sd) && src matches .mem .. then suffix gw else ""

def avxOperation {w} (aw : Width) (op : AvxOperation w) : String :=
  let two (mn : String) {w} (d x : AvxRegOrMem w) := s!"{mn} {avxRm aw x}, {avxRm aw d}"
  match op with
  | .mov op d x => two (Mnemonic.name op) d x
  | .vmov op d x => two s!"v{Mnemonic.name op}" d x
  | .sseMovs op d x => two (Mnemonic.name op) d x
  | .vexMovs op d x => two s!"v{Mnemonic.name op}" d x
  | .sse op d x => s!"{Mnemonic.name op} {avxRm aw x}, {avxReg d}"
  | .vex op d a x => s!"v{Mnemonic.name op} {avxRm aw x}, {avxReg a}, {avxReg d}"
  | .sseUn op d x => s!"{Mnemonic.name op} {avxSrc aw (op.memBytes? w.bytes) x}, {avxReg d}"
  | .vexUn op d x => s!"v{Mnemonic.name op} {avxSrc aw (op.memBytes? w.bytes) x}, {avxReg d}"
  | .sseUnImm op d x i | .sseImm op d x i =>
    s!"{Mnemonic.name op} ${const i}, {avxRm aw x}, {avxReg d}"
  | .vexUnImm op d x i => s!"v{Mnemonic.name op} ${const i}, {avxRm aw x}, {avxReg d}"
  | .vexImm op d a x i => s!"v{Mnemonic.name op} ${const i}, {avxRm aw x}, {avxReg a}, {avxReg d}"
  | .sseShift op d c => s!"{Mnemonic.name op} {simdCount aw c}, {avxReg d}"
  | .vexShift op d a c => s!"v{Mnemonic.name op} {simdCount aw c}, {avxReg a}, {avxReg d}"
  | .sseTest op a x => s!"{Mnemonic.name op} {avxRm aw x}, {avxReg a}"
  | .vexTest op a x => s!"v{Mnemonic.name op} {avxRm aw x}, {avxReg a}"
  | .sseBlendv op d x => s!"{Mnemonic.name op} %xmm0, {avxRm aw x}, {avxReg d}"
  | .vexBlendv op d a x m => s!"v{Mnemonic.name op} {avxReg m}, {avxRm aw x}, {avxReg a}, {avxReg d}"
  | .fma op d a x => s!"v{Mnemonic.name op} {avxRm aw x}, {avxReg a}, {avxReg d}"
  | .vzeroupper => "vzeroupper"
  | .vzeroall => "vzeroall"
  | .vexScalar op dst src1 src2 => s!"v{Mnemonic.name op} {avxReg src2}, {avxReg src1}, {avxReg dst}"
  | .vextract op dst src imm => s!"v{Mnemonic.name op} ${const imm}, {avxReg src}, {avxRm aw dst}"
  | .vinsert op dst src1 src2 imm => s!"v{Mnemonic.name op} ${const imm}, {avxRm aw src2}, {avxReg src1}, {avxReg dst}"
  | .vcvtps2ph dst src imm => s!"vcvtps2ph ${const imm}, {avxReg src}, {avxRm aw dst}"
  | .sseToGpr op d x => s!"{Mnemonic.name op} {avxSrc aw op.memBytes? x}, {reg d}"
  | .vexToGpr op d x => s!"v{Mnemonic.name op} {avxSrc aw op.memBytes? x}, {reg d}"
  | .sseExtract op d x imm =>
    let immStr := optImm imm
    s!"{Mnemonic.name op} {immStr}{avxReg x}, {rm aw d}"
  | .vexExtract op d x imm =>
    let immStr := optImm imm
    s!"v{Mnemonic.name op} {immStr}{avxReg x}, {rm aw d}"
  | .sseInsert op d src imm =>
    let immStr := optImm imm
    s!"{Mnemonic.name op}{cvtSuffix op src} {immStr}{rm aw src}, {avxReg d}"
  | .vexInsert op d a src imm =>
    if op.twoOperand then
      s!"v{Mnemonic.name op} {rm aw src}, {avxReg d}"
    else
      let immStr := optImm imm
      s!"v{Mnemonic.name op}{cvtSuffix op src} {immStr}{rm aw src}, {avxReg a}, {avxReg d}"

def instr : Instr → String
  | .regular aw _ op => operation aw op
  | .avx aw _ op => avxOperation aw op

def directive : Directive → String
  | .instr i => instr i
  | .label l => s!"{l}:"
  | d@(.byteArray _) => toString d

end Kraken.X64.ATT

/-- Print a program in AT&T syntax, one directive per line. -/
def Kraken.X64.toATT (p : Program) : String :=
  "\n".intercalate (p.map ATT.directive)
